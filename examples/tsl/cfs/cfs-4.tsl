//-- State: next, vruntime1, vruntime2
//-- Inputs: enqueue, dequeue
// A simplified version of the Linux Completely Fair Scheduler.
//#LIA#

assume {
  eq next i0();
  eq vruntime1 i0();
  eq vruntime2 i0();
}

always assume {
    eq enqueue i1() || eq enqueue i2();
    eq dequeue i1() || eq dequeue i2();
    !(eq enqueue i1() && eq dequeue i1());
    !(eq enqueue i2() && eq dequeue i2());
}

guarantee {
    ![next <- i1()] W eq enqueue i1();
    ![next <- i2()] W eq enqueue i2();
}

always guarantee {
    ge vruntime1 i0(); /* INV */
    le vruntime1 i15(); /* INV */
    ge vruntime2 i0(); /* INV */
    le vruntime2 i15(); /* INV */

    [next <- i1()] || [next <- i2()] || [next <- i0()];

    eq enqueue i1() -> (F [next <- i1()]) || F eq dequeue i1();
    eq enqueue i2() -> (F [next <- i2()]) || F eq dequeue i2();

    eq dequeue i1() -> ![next <- i1()] W eq enqueue i1();
    eq dequeue i2() -> ![next <- i2()] W eq enqueue i2();

    eq enqueue i2() -> ((gt vruntime1 vruntime2 -> ![next <- i1()]) W eq dequeue i2());
    eq enqueue i1() -> ((gt vruntime2 vruntime1 -> ![next <- i2()]) W eq dequeue i1());

    [next <- i1()] <-> [vruntime1 <- add vruntime1 i1()];
    [next <- i2()] <-> [vruntime2 <- add vruntime2 i1()];
}
