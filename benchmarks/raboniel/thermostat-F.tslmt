//-- State:  temp, mode 
//-- Inputs: sense

// target temp range
LBOUND = i20();
UBOUND = i25();

inRangeSense = ((ge sense LBOUND) && (le sense UBOUND));
inRangeTemp = ((ge temp LBOUND) && (le temp UBOUND));

// temp change
TUNIT = i1();

// modes
OFF = i0();
HEAT = i1();
COOL = (sub i0() i1());

modeOFF = (eq mode OFF);
modeHEAT = (ge mode HEAT);
modeCOOL = (le mode COOL);


assume {
    modeOFF;
}

always guarantee {
    
    // mode switching
    (modeOFF && !inRangeSense) -> ([mode <- HEAT] || [mode <- COOL]);  
    (modeOFF && inRangeSense) ->  [mode <- OFF];  
    (!modeOFF && !inRangeTemp) -> [mode <- mode];
    (!modeOFF && inRangeTemp)  -> [mode <- OFF];
    
    // temp control
    modeOFF -> [temp <- sense];
    (modeHEAT && !inRangeTemp) -> [temp <- add temp TUNIT];
    (modeCOOL && !inRangeTemp) -> [temp <- sub temp TUNIT];
    !modeOFF && inRangeTemp -> [temp <- temp];
}

guarantee{
    // eventually off
    X F modeOFF;
}
