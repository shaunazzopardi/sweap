var Int x
var Int tx
var Bool deliver
var Bool hasPackage

var Int mode

inp Int customerPos
inp Int stationPos

SPECIFICATION

toCustomer = [mode <- i0()];
toStation = [mode <- i1()];

guarantee {
    
    // Movement
    G (deliver && lt x tx -> [x <- add x i1()]);
    G (deliver && gt x tx -> [x <- sub x i1()]);
    G (deliver && eq x tx -> [x <- x]);
   
    G (deliver && eq x tx -> [deliver <- false]);
    G (deliver && !eq x tx -> [deliver <- true]);
  
    // Liveness condition
    G (deliver -> F eq x tx);

    // Target allocation and package logic
    toCustomer || toStation;

    // System does not switch recipients
    G (toCustomer -> (toCustomer W !deliver));
    G (toStation -> (toStation W !deliver));

    // System does not loose packages or the target
    G (deliver -> [hasPackage <- hasPackage]);
    G (deliver -> [tx <- tx]);
    
    // Realocation of targets
    G (toCustomer && !deliver -> [deliver <- true] && [tx <- customerPos] && X (toStation || toCustomer));
    G (toStation && !deliver ->  [deliver <- true] && [tx <- stationPos] && X toCustomer);

    // Package pickup and leaving
    G (toCustomer && !deliver -> hasPackage && [hasPackage <- false]);
    G (toStation && !deliver -> [hasPackage <- true]);
}
