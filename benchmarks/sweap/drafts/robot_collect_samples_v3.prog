program robot_collect_samples_v1 {
    STATES {
        base, collectSamples, moveToSampleSide, moveToBase : init
    }

    ENVIRONMENT EVENTS {
        e, f, stop
    }

    CONTROLLER EVENTS {
        a, b
    }

    VALUATION {
        samples : integer := 0;
        samplesA : integer := 0;
        samplesB : integer := 0;
        trackPos : integer := 0;
        reqSamples : integer := 0;
    }

    TRANSITIONS {
        moveToBase -> base [trackPos == 23 $ samples := 0],
        moveToBase -> moveToBase [trackPos != 23 & a $],
        moveToBase -> moveToBase [trackPos != 23 & !a & b $ trackPos := trackPos + 1],
        moveToBase -> moveToBase [trackPos != 23 & !a & !b $ trackPos := trackPos - 1],

        base -> base [e $ reqSamples := reqSamples + 1],
        base -> moveToSampleSide [!e & reqSamples > 0],

        moveToSampleSide -> moveToSampleSide [trackPos != 12 & !a & b $ trackPos := trackPos + 1],
        moveToSampleSide -> moveToSampleSide [trackPos != 12 & !a & !b $ trackPos := trackPos - 1],
        moveToSampleSide -> moveToSampleSide [trackPos != 12 & a],
        moveToSampleSide -> collectSamples [trackPos == 12],

        collectSamples -> moveToBase [!(samples < reqSamples)],
        collectSamples -> collectSamples [samples < reqSamples & !stop & e],
        collectSamples -> collectSamples [samples < reqSamples & !stop & !e & f $ samplesA := samplesA + 1],
        collectSamples -> collectSamples [samples < reqSamples & !stop & !e & !f $ samplesB := samplesB + 1],
        collectSamples -> collectSamples [samples < reqSamples & stop & (samplesA > 0 | samplesB > 0) & a $ samples := samples + samplesA; samplesA := 0; samplesB := 0],
        collectSamples -> collectSamples [samples < reqSamples & stop & (samplesA > 0 | samplesB > 0) & !a $ samples := samples + samplesB; samplesA := 0; samplesB := 0]
    }

    SPECIFICATION {
        (G F stop) -> G F base
    }
}