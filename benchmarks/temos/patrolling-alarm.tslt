#LIA
//-- State:  pos, timer
//-- Inputs: alarm1i, alarm2i

alarm1 = eq alarm1i int0();
alarm2 = eq alarm2i int0();

L1 = int1();
L2 = int2();
BASE = int0();

DONE = (gte timer int60());

assume {
    (eq pos BASE);
    (eq timer int0());
}


guarantee {

    // position update

    G ((eq pos BASE) || (eq pos L1) || (eq pos L2));

    G ((eq pos BASE) -> [pos <- BASE] || [pos <- L1] || [pos <- L2]) ;
    G ((eq pos L1) -> [pos <- BASE]);
    G ((eq pos L2) -> [pos <- BASE]);
    

    // do not leave BASE untill DONE

    G ((eq pos BASE) && !DONE && !alarm1 && !alarm2 -> [pos <- BASE]);
    G ((eq pos BASE) && DONE -> [pos <- L1] || [pos <- L2]);
    
        
    // timer update
    G (gte timer int0());
    G ( (eq pos BASE) -> [timer <- add timer int1()]);
    G (!(eq pos BASE) -> [timer <- int0()]);


    // objectives 

    G F ((gt pos BASE));
    G (alarm1 && (eq pos BASE) -> X (eq pos L1));
    G (!alarm1 && alarm2 && (eq pos BASE) -> X (eq pos L2));

}
